buildscript {
    ext {
        scala_version = '2.11'
        spark_version = '2.2.2'
        spark_dependencies = [
                "org.apache.spark:spark-core_$scala_version:$spark_version",
                "org.apache.spark:spark-sql_$scala_version:$spark_version",
                "org.apache.spark:spark-mllib_$scala_version:$spark_version"
        ]
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

plugins {
    id 'application' // support mainClassName
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '2.0.4' // add shadowJar task
}

group 'sarda'
version '1.0-SNAPSHOT'

mainClassName = 'hello.spark.Main'

repositories {
    mavenCentral()
}

dependencies {
    // excluded from shadowJar
    //noinspection GroovyAssignabilityCheck
    compileOnly(
            *spark_dependencies
    )
    implementation (
            "org.scala-lang:scala-library:$scala_version"
    )
    // *implementation, so bundled into (test) jar
    //noinspection GroovyAssignabilityCheck
    testImplementation (
            "junit:junit:4.12",
            "org.scalatest:scalatest_$scala_version:3.0.+",
            // spark again so that local tests can be executed from IntelliJ
            *spark_dependencies
    )
}

// shadowJar task creates a jar in build/libs that can be executed with:
// spark-submit --master local[1] --files spark-warehouse/questions_10K.csv,spark-warehouse/question_tags_10K.csv build/libs/hello-spark-gradle-1.0-SNAPSHOT-all.jar
